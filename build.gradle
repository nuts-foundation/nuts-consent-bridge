/*
 * Nuts consent bridge
 * Copyright (C) 2019 Nuts community
 *
 *  This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */


buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        kotlinVersion = '1.2.71'
        cordaVersion = '4.4'
        springBootVersion = '2.1.11.RELEASE'
        openApiPluginVersion = '3.3.4'
        nutsVersion = project.version
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.openapitools:openapi-generator-gradle-plugin:$openApiPluginVersion"
    }
}

ext {
    jacksonVersion = '2.9.8'
    slf4jVersion = '1.7.25'
    log4jVersion = '2.9.1'
    wiremockVersion = '2.23.2'
    stanVersion = '2.2.3'
    natsVersion = '2.6.7'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://software.r3.com/artifactory/corda"
        }
        maven {
            url "https://repo.gradle.org/gradle/libs-releases/"
        }
    }

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    // exclude unneeded libs
    configurations {
        implementation {
            exclude group: 'com.github.corda.crash'
            exclude group: 'com.github.bft-smart', module: 'library'
            exclude group: 'ch.qos.logback'
            exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        }

        testImplementation {
            exclude group: 'com.github.corda.crash'
            exclude group: 'com.github.bft-smart', module: 'library'
        }
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        implementation("org.springframework.boot:spring-boot-starter-data-rest:${springBootVersion}") {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        }
        implementation("org.springframework.boot:spring-boot-starter-jetty:${springBootVersion}")

        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
        testImplementation 'org.liquibase:liquibase-core:3.8.2'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true   // Useful for reflection.
        }
    }

    def excludePackagePattern = "x(antlr**;bftsmart**;ch**;co.paralleluniverse**;com.codahale**;com.esotericsoftware**;" +
            "com.fasterxml**;com.google**;com.ibm**;com.intellij**;com.jcabi**;com.nhaarman**;com.opengamma**;" +
            "com.typesafe**;com.zaxxer**;de.javakaffee**;groovy**;groovyjarjarantlr**;groovyjarjarasm**;io.atomix**;" +
            "io.github**;io.netty**;jdk**;joptsimple**;junit**;kotlin**;net.corda.djvm**;djvm.**;net.bytebuddy**;" +
            "net.i2p**;org.apache**;" +
            "org.assertj**;org.bouncycastle**;org.codehaus**;org.crsh**;org.dom4j**;org.fusesource**;org.h2**;" +
            "org.hamcrest**;org.hibernate**;org.jboss**;org.jcp**;org.joda**;org.junit**;org.mockito**;org.objectweb**;" +
            "org.objenesis**;org.slf4j**;org.w3c**;org.xml**;org.yaml**;reflectasm**;rx**;org.jolokia**;" +
            "com.lmax**;picocli**;liquibase**;com.github.benmanes**;org.json**;org.postgresql**;nonapi.io.github.classgraph**;)"

    test {
        jvmArgs "-javaagent:${project.rootDir}/lib/quasar-core-0.7.12_r3.jar=${excludePackagePattern}",
                "-Dco.paralleluniverse.fibers.verifyInstrumentation=false"
        environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "test"
        enableAssertions = false
    }
}

dependencies {
    implementation project(":impl")
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco/html")
        csv.enabled false
    }

    subprojects.each {
        sourceSets it.sourceSets.main
    }
}

//change to dependsOn check when IT are added
codeCoverageReport.dependsOn {
    subprojects*.test
}

apply plugin: 'org.openapi.generator'

check.dependsOn jacocoTestReport

task generateServerApiStub(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "kotlin-spring"
    outputDir = "${project.rootDir}/generated"
    inputSpec = "docs/_static/nuts-consent-bridge.yaml"
    verbose = false
    apiPackage = "nl.nuts.consent.bridge.api"
    modelPackage = "nl.nuts.consent.bridge.model"
    invokerPackage = "nl.nuts.consent.bridge"
    generateApiDocumentation = false
    generateModelDocumentation = false
    generateModelTests = false
    generateApiTests = false
    withXml = false
    configOptions = [
            dateLibrary: "java8",
            serviceInterface: true,
            exceptionHandler: true,
            gradleBuildFile: false,
            supportingFiles: []
    ]
    skipOverwrite = false
}

task generateRegistryClientApiCode(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "kotlin"
    groupId = "${project.group}"
    id = "registry-client"
    outputDir = "${project.rootDir}/generated-registry-client"
    inputSpec = "https://nuts-documentation.readthedocs.io/en/latest/_static/nuts-registry.yaml"
    verbose = false
    generateApiDocumentation = false
    generateModelDocumentation = false
    generateModelTests = false
    generateApiTests = false
    withXml = false
    configOptions = [
            packageName: "nl.nuts.consent.bridge.registry",
            dateLibrary: "java8",
            exceptionHandler: true,
            gradleBuildFile: false,
            supportingFiles: []
    ]
    skipOverwrite = false
}


task generateEventStoreClientApiCode(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "kotlin"
    groupId = "${project.group}"
    id = "event-store-client"
    outputDir = "${project.rootDir}/generated-event-store-client"
    inputSpec = "https://nuts-documentation.readthedocs.io/en/latest/_static/nuts-event-store.yaml"
    verbose = false
    generateApiDocumentation = false
    generateModelDocumentation = false
    generateModelTests = false
    generateApiTests = false
    withXml = false
    configOptions = [
            packageName: "nl.nuts.consent.bridge.events",
            dateLibrary: "java8",
            exceptionHandler: true,
            gradleBuildFile: false,
            supportingFiles: []
    ]
    skipOverwrite = false
}

openApiValidate {
    inputSpec = "../nuts-api-spec/docs/_static/nuts-consent-bridge.yaml"
}

openApiGenerate {

}

task codegen(dependsOn: ['generateServerApiStub', 'generateRegistryClientApiCode', 'generateEventStoreClientApiCode'])